dnl $Id$
dnl
dnl Copyright (c) 2006 - Jannis Pohlmann <jannis@xfce.org>
dnl

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2006 - Jannis Pohlmann <jannis@xfce.org>])
AC_INIT([panel-plugin/verve-plugin.c], [0.1.0], [jannis@xfce.org], [verve-plugin])
AC_REVISION([$Id$])
AC_PREREQ([2.50])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([AC_PACKAGE_TARNAME()], [AC_PACKAGE_VERSION()])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()

dnl *******************************
dnl *** Check for UNIX variants ***
dnl *******************************
AC_AIX()
AC_ISC_POSIX()
AC_MINIX()

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AC_PROG_LD()
AC_PROG_INSTALL()
AC_PROG_INTLTOOL()

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
AC_DISABLE_STATIC()
AC_PROG_LIBTOOL()

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([de])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([LIBEXO], [exo-0.3], [0.3.1.3])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.3.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.3.0])

dnl **************************
dnl *** Check for libpcre3 ***
dnl **************************
LIBPCRE_LIBS=""
LIBPCRE_CFLAGS=""
AC_CHECK_LIB([pcre], [pcre_compile],
[
  AC_CHECK_HEADER([pcre.h],
  [
    LIBPCRE_LIBS="-lpcre -lm"
    AC_DEFINE([HAVE_LIBPCRE], [1], [Define to 1 if libpcre is found])
    AC_DEFINE([HAVE_PCRE_H], [1], [Define to 1 if pcre.h is found])
  ])
], [-lm])
AC_SUBST([LIBPCRE_CFLAGS])
AC_SUBST([LIBPCRE_LIBS])

dnl ******************************
dnl *** Configure panel plugin ***
dnl ******************************
XFCE_PANEL_PLUGIN([XFCE4_PANEL], [4.3.0])

dnl *************************************************
dnl *** Optional support for startup notification ***
dnl *************************************************
XDT_CHECK_OPTIONAL_PACKAGE([LIBSTARTUP_NOTIFICATION],
                           [libstartup-notification-1.0],
                           [0.4], [startup-notification],
                           [startup notification library])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
BM_DEBUG_SUPPORT()

AC_OUTPUT([
Makefile
panel-plugin/Makefile
po/Makefile.in
])
